apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

android {
    compileSdk 33

    defaultConfig {
        applicationId "com.leos.pic.smart.gallery"
        minSdk 21
        //noinspection OldTargetApi
        targetSdk 31
        versionCode 1
        versionName "0.0.1"
        setProperty("archivesBaseName", "ca.on.sudbury.hojat.smartgallery-$versionCode")
        vectorDrawables.useSupportLibrary = true
    }

    signingConfigs {
        if (keystorePropertiesFile.exists()) {
            release {
                keyAlias keystoreProperties['keyAlias']
                keyPassword keystoreProperties['keyPassword']
                storeFile file(keystoreProperties['storeFile'])
                storePassword keystoreProperties['storePassword']
            }
        }
    }

    buildTypes {
        debug {
            // we cannot change the original package name, else PhotoEditorSDK won't work
            //applicationIdSuffix ".debug"
        }
    }

    buildFeatures {
        viewBinding true
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        if (is_proprietary) {
            main.java.srcDirs += 'src/proprietary/kotlin'
        }
    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    packagingOptions {
        exclude 'META-INF/library_release.kotlin_module'
    }
}

dependencies {

    implementation project(":fingerprint")
    implementation project(":renderer")
    implementation project(":palette")

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "androidx.constraintlayout:constraintlayout:2.1.2"
    implementation "androidx.documentfile:documentfile:1.0.1"
    implementation "androidx.exifinterface:exifinterface:1.3.3"
    implementation "androidx.biometric:biometric-ktx:1.2.0-alpha03"
    api "androidx.core:core-ktx:1.6.0"
    api "androidx.appcompat:appcompat:1.3.1"
    api "com.google.code.gson:gson:2.8.8"
    api "com.github.bumptech.glide:glide:4.13.2"
    annotationProcessor "com.github.bumptech.glide:compiler:4.13.2"

    // google material library
    implementation "com.google.android.material:material:1.6.1"

    // Timber logging
    implementation "com.jakewharton.timber:timber:5.0.1"
    // All the kotlin extensions for Fragment
    implementation "androidx.fragment:fragment-ktx:1.5.2"
    implementation "androidx.biometric:biometric:1.0.1"
    implementation "androidx.constraintlayout:constraintlayout:2.1.4"
    implementation "com.google.android.exoplayer:exoplayer-core:2.9.6"
    implementation "com.google.vr:sdk-panowidget:1.180.0"
    implementation "com.google.vr:sdk-videowidget:1.180.0"
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:1.1.0"
    implementation "com.squareup.okio:okio:3.0.0"
    implementation("com.squareup.picasso:picasso:2.71828") {
        exclude group: "com.squareup.okhttp3", module: "okhttp"
    }
    compileOnly "com.squareup.okhttp3:okhttp:4.10.0"
    kapt "com.github.bumptech.glide:compiler:4.13.2"
    kapt "androidx.room:room-compiler:2.4.3"
    implementation "androidx.room:room-runtime:2.4.3"
    annotationProcessor "androidx.room:room-compiler:2.4.3"


    // TODO the ViewPagers of the app should be overwritten in viewpager2
    api 'com.duolingo.open:rtl-viewpager:1.0.3'
    implementation "androidx.viewpager2:viewpager2:1.0.0"

    // Date/Time formatting
    api 'joda-time:joda-time:2.10.13'

    // Gif viewer (written in C)
    implementation 'pl.droidsonroids.gif:android-gif-drawable:1.2.25'

    // Apache Sanselan is a pure-Java image library. Only jar files are available at
    // (https://search.maven.org/artifact/org.apache.sanselan/sanselan/0.97-incubator/bundle)
    implementation 'org.apache.sanselan:sanselan:0.97-incubator'

    // Hosted at (https://github.com/emonm/AndroidPhotoFilters)
    // search for "zomato" in code base
    implementation 'info.androidhive:imagefilters:1.0.7'

    // these 2 libraries are written in C++
    // Hosted at (https://github.com/penfeizhou/APNG4Android)
    implementation 'com.github.penfeizhou.android.animation:awebp:2.17.0'
    implementation 'com.github.penfeizhou.android.animation:apng:2.17.0'

}

// Apply the PESDKPlugin
if (is_proprietary) {
    apply plugin: 'ly.img.android.sdk'

    imglyConfig {
        vesdk {
            enabled true
            licensePath 'vesdk_license'
        }

        pesdk {
            enabled true
            licensePath 'pesdk_license'
        }

        modules {
            include 'ui:video-trim'
            include 'ui:core'
            include 'ui:text'
            include 'ui:focus'
            include 'ui:brush'
            include 'ui:filter'
            include 'ui:sticker'
            include 'ui:overlay'
            include 'ui:transform'
            include 'ui:adjustment'

            include 'backend:serializer'
            include 'backend:sticker-smart'
            include 'backend:sticker-animated'

            include 'assets:font-basic'
            include 'assets:filter-basic'
            include 'assets:overlay-basic'
            include 'assets:sticker-shapes'
            include 'assets:sticker-emoticons'
            include 'assets:sticker-animated'
        }
    }
}
